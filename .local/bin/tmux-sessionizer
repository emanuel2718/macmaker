#!/usr/bin/env fish

function search
	find "$PROJECTS" -maxdepth 1 -type d |
		while read p
			zoxide query -l -s "$p/"
		end |
		sed "s;$PROJECTS/;;" |
		grep -v "$PROJECTS" |
		grep "/" |
		sort -rnk1 |
		uniq |
		awk '{print $2}' |
		fzf --no-sort --prompt "> " --preview "cat $PROJECTS/{}/README.md"
end

if test (count $argv) -eq 1
	set selected "$argv[1]"
else
	set selected $(search)
end

if test -z $selected
    exit 0
end

set selected_name (basename "$selected" | tr . _)
set tmux_running (pgrep tmux)

if test -z "$TMUX" && test -z "$tmux_running"
    tmux new-session -s "$selected_name" -c "$PROJECTS/$selected"
    exit 0
end

if ! tmux has-session -t="$selected_name" 2> /dev/null
    tmux new-session -ds "$selected_name" -c "$PROJECTS/$selected"
end

tmux switch-client -t "$selected_name"
